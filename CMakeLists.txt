
# Set up the project.
cmake_minimum_required( VERSION 3.10 )
project( CUDALinkTest VERSION 1.0.0 LANGUAGES CXX CUDA )

# Use C++14.
set( CMAKE_CXX_STANDARD 14 CACHE STRING
   "The C++ version to use" )
set( CMAKE_CXX_EXTENSIONS OFF CACHE BOOL
   "(Dis)Allow C++ compiler extensions" )

# Use the GNU default directory names for installation.
include( GNUInstallDirs )

# Build a static library with the "helper code".
add_library( HelperLib STATIC HelperLib/Macros.cuh
   HelperLib/HelperClass.cuh HelperLib/HelperClass.cu
   HelperLib/HelperClassPrinter.h HelperLib/HelperClassPrinter.cu )
set_target_properties( HelperLib PROPERTIES
   CUDA_SEPARABLE_COMPILATION ON
   POSITION_INDEPENDENT_CODE  ON )
set_property( TARGET HelperLib PROPERTY PUBLIC_HEADER
   HelperLib/Macros.cuh HelperLib/HelperClass.cuh
   HelperLib/HelperClassPrinter.h )
target_include_directories( HelperLib PUBLIC
   $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/HelperLib>
   $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}> )

# Build a shared library with the "user code".
add_library( MultiplyLib SHARED MultiplyLib/ArrayMultiply.h
   MultiplyLib/ArrayMultiply.cu MultiplyLib/PrepareObject.h
   MultiplyLib/PrepareObject.cu MultiplyLib/Executor.cuh )
set_target_properties( MultiplyLib PROPERTIES
   CUDA_SEPARABLE_COMPILATION ON )
set_property( TARGET MultiplyLib PROPERTY PUBLIC_HEADER
   MultiplyLib/ArrayMultiply.h MultiplyLib/PrepareObject.h )
target_link_libraries( MultiplyLib PRIVATE HelperLib )
target_include_directories( MultiplyLib PUBLIC
   $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/MultiplyLib>
   $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}> )

# Build the test executable using both libraries.
add_executable( testComplexLink main.cxx )
target_link_libraries( testComplexLink PRIVATE
   MultiplyLib HelperLib )

# Build the test executable using just the static library.
add_executable( testSimpleLink main.cxx
   MultiplyLib/ArrayMultiply.h MultiplyLib/ArrayMultiply.cu
   MultiplyLib/PrepareObject.h MultiplyLib/PrepareObject.cu
   MultiplyLib/Executor.cuh )
set_target_properties( testSimpleLink PROPERTIES
   CUDA_SEPARABLE_COMPILATION ON )
target_include_directories( testSimpleLink PRIVATE
   ${CMAKE_SOURCE_DIR}/MultiplyLib )
target_link_libraries( testSimpleLink PRIVATE HelperLib )

# Install all targets.
install( TARGETS HelperLib MultiplyLib testComplexLink testSimpleLink
   RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
   LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
   ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
   PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR} )
